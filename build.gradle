group 'com.heapix'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.1.RELEASE")
        classpath 'mysql:mysql-connector-java:5.1.34'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

bootJar {
    baseName = 'gs-spring-boot'
    version = '0.1.0'
}

tasks.withType(JavaCompile) {
    options.compilerArgs = [
            '-Amapstruct.suppressGeneratorTimestamp=true'
    ]
}


repositories {
    mavenCentral()
}

sourceCompatibility = 1.8

def webappDir = "$projectDir/src/main/resources/webapp"

processResources {
    dependsOn "processVue"
}

task processVue(type: Copy) {
    dependsOn "buildVue"
    from "${webappDir}/dist"
    into "${buildDir}/resources/main/static"
}

task buildVue(type: Exec) {
    dependsOn "installVue"
    workingDir "$webappDir"
    inputs.dir "$webappDir"
    group = BasePlugin.BUILD_GROUP
    commandLine "npm", "run", "build"
}

task installVue (type: Exec) {
    workingDir "$webappDir"
    inputs.dir "$webappDir"
    group = BasePlugin.BUILD_GROUP
    commandLine "npm", "install"
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile group: 'org.springframework.security', name: 'spring-security-jwt', version: '1.0.9.RELEASE'
    compile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.0'
    testCompile 'junit:junit'
    compile 'io.springfox:springfox-swagger2:2.7.0'
    compile 'io.springfox:springfox-swagger-ui:2.7.0'
    compileOnly 'org.projectlombok:lombok'
    runtime('mysql:mysql-connector-java')
}

